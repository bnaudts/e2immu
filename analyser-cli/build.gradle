/*
 * e2immu: a static code analyser for effective and eventual immutability
 * Copyright 2020-2021, Bart Naudts, https://www.e2immu.org
 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
 * more details. You should have received a copy of the GNU Lesser General Public
 * License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group 'org.e2immu'
version '0.6.0'

dependencies {
    implementation group: 'org.e2immu', name: 'e2immu-support', version: '0.6.0'      // LGPL 3.0

    implementation project(':analyser')
    implementation project(':analyser-store-uploader')

    implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.18.0'

    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4' // Apache License 2.0
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'  // MIT X-11 License

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'           // EPL v2 License
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

application {
    mainClass.set('org.e2immu.analyser.cli.Main')
}

jar {
    manifest {
        attributes "Main-Class": "org.e2immu.analyser.cli.Main"
    }
}

// ********************************* Publishing

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/e2immu/e2immu")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'org.e2immu'
            artifactId = 'analyser-cli'
            from components.java

            pom {
                name = 'e2immu analyser command line'
                description = 'The command line interface of the e2immu static code analyser'
                url = 'http://www.e2immu.org'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'bnaudts'
                        name = 'Bart Naudts'
                        email = 'bart.naudts@e2immu.org'
                    }
                }
            }
        }
        mavenJava(MavenPublication) {
            groupId = 'org.e2immu'
            artifactId = 'analyser-cli'
            from components.java
        }
    }
}


