/*
 * e2immu: a static code analyser for effective and eventual immutability
 * Copyright 2020-2021, Bart Naudts, https://www.e2immu.org
 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
 * more details. You should have received a copy of the GNU Lesser General Public
 * License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false;
        //html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

jar {
    from sourceSets.main.java
}

test {
    exclude '**/parserfailing/*.class'

    maxHeapSize = "2G"
    useJUnitPlatform()
}

dependencies {
    implementation 'org.e2immu:e2immu-support:0.3.0'      // LGPL 3.0

   // implementation 'org.slf4j:slf4j-api:1.7.32'           // MIT X-11 License
    implementation 'ch.qos.logback:logback-classic:1.2.9' // EPL v1.0 and the LGPL 2.1
    implementation 'commons-io:commons-io:20030203.000550'            // Apache License 2.0

    implementation 'com.github.javaparser:javaparser-core:3.22.2-SNAPSHOT'

    // LGPL or Apache License 2.0
    implementation 'org.ow2.asm:asm:9.2'                  // 3-clause BSD permissive license

    // TEST
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'                    // EPL v2.0
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

version = '0.3.0'
group 'org.e2immu'

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/e2immu/e2immu")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'org.e2immu'
            artifactId = 'analyser'
            from components.java

            pom {
                name = 'e2immu analyser'
                description = 'The e2immu static code analyser'
                url = 'http://www.e2immu.org'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'bnaudts'
                        name = 'Bart Naudts'
                        email = 'bart.naudts@e2immu.org'
                    }
                }
            }
        }
        mavenJava(MavenPublication) {
            groupId = 'org.e2immu'
            artifactId = 'analyser'
            from components.java
        }
    }
}



